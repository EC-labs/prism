#!/home/diogo/.local/bin/bpftrace
#include <linux/stat.h>
#include <linux/fs.h>

BEGIN 
{
    @pids[$1] = 1;
}

kfunc:vmlinux:vfs_read
{
    $inode = (struct inode *)args->file->f_inode;
    $mode = $inode->i_mode;

    if (($mode & S_IFMT) == S_IFIFO) {
        $i_id = $inode->i_ino;
        $s_id = $inode->i_sb->s_dev;

        if (!@pipes[$s_id, $i_id]) {
            if (@pids[pid]) {
                @pipes[$s_id, $i_id] = 1;
            }
        } 

        if (!@pipes[$s_id, $i_id]) {
            return;
        }

        if (!@pids[pid]) {
            printf("%s\t%lld\n", 
                   "NewProcess", pid);
            @pids[pid] = 1
        }

        @start_fifo[tid, $s_id, $i_id] = nsecs;
        printf("%s\t%lld\t%lld\t%lld\t%lld\n", 
               "FifoReadStart", tid, $s_id, $i_id, @start_fifo[tid, $s_id, $i_id]);
    } else if (($mode & S_IFMT) == S_IFREG) {
        if (!@pids[pid]) {
            return;
        }
        @start_regular[tid] = nsecs;
    }
}

kretfunc:vmlinux:vfs_read 
{
    if (!@pids[pid]) {
        return;
    }

    $inode = (struct inode *)args->file->f_inode;
    $mode = $inode->i_mode;

    if (($mode & S_IFMT) == S_IFIFO) {
        $i_id = $inode->i_ino;
        $s_id = $inode->i_sb->s_dev;

        if (!@start_fifo[tid, $s_id, $i_id]) {
            return;
        }

        printf("%s\t%lld\t%lld\t%lld\t%lld\n", 
               "FifoReadEnd", tid, $s_id, $i_id, nsecs - @start_fifo[tid, $s_id, $i_id]);
        delete(@start_fifo[tid, $s_id, $i_id]);
    } else if (($mode & S_IFMT) == S_IFREG) {
        if (!@start_regular[tid]) {
            return;
        }
        
        @hist_regular["r", tid] = hist(nsecs - @start_regular[tid]);
    }
    
}

kfunc:vmlinux:vfs_write
{
    $inode = (struct inode *)args->file->f_inode;
    $mode = $inode->i_mode;

    if (($mode & S_IFMT) == S_IFIFO) {
        $i_id = $inode->i_ino;
        $s_id = $inode->i_sb->s_dev;

        if (!@pipes[$s_id, $i_id]) {
            if (@pids[pid]) {
                @pipes[$s_id, $i_id] = 1;
            }
        } 

        if (!@pipes[$s_id, $i_id]) {
            return;
        }

        if (!@pids[pid]) {
            printf("%s\t%lld\n", 
                   "NewProcess", pid);
            @pids[pid] = 1
        }

        @start_fifo[tid, $s_id, $i_id] = nsecs;
        printf("%s\t%lld\t%lld\t%lld\t%lld\n", 
               "FifoWriteStart", tid, $s_id, $i_id, @start_fifo[tid, $s_id, $i_id]);
    } else if (($mode & S_IFMT) == S_IFREG) {
        if (!@pids[pid]) {
            return;
        }

        @start_regular[tid] = nsecs;
    }
}

kretfunc:vmlinux:vfs_write 
{
    if (!@pids[pid]) {
        return;
    }

    $inode = (struct inode *)args->file->f_inode;
    $mode = $inode->i_mode;

    if (($mode & S_IFMT) == S_IFIFO) {
        $i_id = $inode->i_ino;
        $s_id = $inode->i_sb->s_dev;

        if (!@start_fifo[tid, $s_id, $i_id]) {
            return;
        }

        printf("%s\t%lld\t%lld\t%lld\t%lld\n", 
               "FifoWriteEnd", tid, $s_id, $i_id, nsecs - @start_fifo[tid, $s_id, $i_id]);
        delete(@start_fifo[tid, $s_id, $i_id]);
    } else if (($mode & S_IFMT) == S_IFREG) {
        if (!@start_regular[tid]) {
            return;
        }
        
        @hist_regular["w", tid] = hist(nsecs - @start_regular[tid]);
    }
}

interval:s:1
{
    print(@hist_regular);
    clear(@hist_regular);
}

END 
{
    clear(@start_fifo);
    clear(@pipes);
    clear(@pids);
    clear(@hist_regular);
    clear(@start_regular);
}
