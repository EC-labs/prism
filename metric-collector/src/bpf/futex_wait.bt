#!/home/diogo/.local/bin/bpftrace
/* Signals when a thread enters a waiting state for a futex, and when it
 * returns from waiting. 
 */

#include <linux/futex.h>

BEGIN {
    printf("%-10s\t%20s\t%20s\t%20s\n", 
           "EVENT_TYPE", "PID", "THREAD-ID", "VALUE");
}

tracepoint:syscalls:sys_enter_futex 
/ pid == $1 /
{
    /* We want to account for all waits */
    $op = args->op^FUTEX_PRIVATE_FLAG;

    if ($op == FUTEX_WAIT_BITSET || $op == FUTEX_WAIT) {
        @start[tid] = nsecs;
        printf("%-10s\t%20lld\t%20lld\t%20lld\n", 
               "start", pid, tid, @start[tid]);
    }
}

tracepoint:syscalls:sys_exit_futex 
/ @start[tid] / 
{
    $diff = (uint64)(nsecs - @start[tid]);
    @start[tid] = 0;
    printf("%-10s\t%20lld\t%20lld\t%20lld\n", 
           "elapsed", pid, tid, $diff);
}

END {
    clear(@start);
}
